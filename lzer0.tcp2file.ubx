#!/bin/tcsh
# Script to stream real-time u-blox GNSS receiver data into files 
# created  by DZ (Dec 2020)
# modified by DZ (Aug 2022)
# modified by sg (mar 25) - Added internal logging



#********************  DEFAULTS ********************
set JOB         = `basename $0`
set CURDIR      = `pwd`
set LOGDIR      = "/home/lzer0/log"
set LOGFILE     = "${LOGDIR}/${JOB}.log"

# Create log directory if it doesn't exist
if (! -d ${LOGDIR}) then
    mkdir -p ${LOGDIR}
endif

# Function to log messages
alias log 'echo "`date "+%Y-%m-%d %H:%M:%S"` - $JOB - $*" >> ${LOGFILE}'

log "Script started"

#********************  INPUT SETTINGS MANAGEMENT ***************
#
# If no arguments are passed it outputs script usage
if (${%argv} == 0) then
        log "No arguments provided"
        echo "- USAGE: $JOB -f [config file]"
        echo "- USAGE: $JOB -s [site name]"
        echo "    e.g: $JOB -f $HOME/cfg/sites.cfg"
        echo "    e.g: $JOB -s L001"
        exit 1
endif

# check command line params.
while($#)
        switch($1)
                case "-f":
                        shift
                        set cfgFile     = $1
                        set siteName    = `cat $cfgFile | grep -v "#" | gawk 'BEGIN{FS=":"} {if ($1=="rover name") {print $2}}' | tr -d " " | tr "a-z" "A-Z"`
                        log "Using config file: $cfgFile, Site name: $siteName"
                        breaksw
                case "-s":
                        shift
                        set siteName    = $1
                        log "Using site name: $siteName"
                        breaksw
                default:
                        breaksw
        endsw
        shift
end

#******************** STARTS STR2STR TO RECORD REAL-TIME UBX STREAM INTO FILE
# SYNTAX
# %Y -> yyyy
# %y -> yy
# %m -> mm
# %d -> dd
# %h -> hh
# %H -> a
# %M -> mm
# %n -> ddd
# %W -> wwww
# %D -> d
# %N -> d
# %s -> ssss
# %S -> SSSS
# %r -> rrrr
log "Starting str2str process"
nohup /usr/local/bin/str2str -in tcpcli://127.0.0.1:2222 -out file:///mnt/hd/gnss/%Y/%n/%H/${siteName}%H%y.%n::S=1::T -f 0 >& /dev/null &
log "str2str process started"

#!/bin/tcsh
# Script to record real-time coordinates (pos format) from a TCP port into hourly files
# created  by DZ (Nov 2018)
# modified by DZ (Nov 2018)
# modified by DZ (Nov 2022)
# modified by DZ (Aug 2022)
#
#********************  DEFAULTS ********************
set JOB         		= `basename $0`
set CURDIR      		= `pwd`
set dumpDir     		= /mnt/hd/gnss
set IP_ADDR     		= 127.0.0.1
set IP_PORT     		= 5754
set RUNNINGSCRIPT               = str2str
#
# If no arguments are passed it outputs script usage
if (${%argv} == 0) then
        echo "- USAGE: $JOB -f [config file]"
        echo "- USAGE: $JOB -s [SITE]"
        echo "    e.g: $JOB -f $HOME/cfg/sites.cfg"
        echo "    e.g: $JOB -s L001"
        exit 1
endif
#
# check command line params.
while($#)
        switch($1)
                case "-s":
                        # Site name
                        shift
                        set SITE = `echo $1 | tr 'a-z' 'A-Z'`
                        breaksw
                case "-f":
                        #  get receiver name from config file
                        shift
                        set cfgFile     = $1
                        set SITE    = `cat $cfgFile | grep -v "#" | gawk 'BEGIN{FS=":"} {if ($1=="rover name") {print $2}}' | tr -d " " | tr "a-z" "A-Z"`
                        breaksw
                default:
                        breaksw
        endsw
        shift
end
set KEYWORD1 = $IP_PORT
set KEYWORD2 = $IP_ADDR
set KEYWORD3 = $SITE
set RUNNINGSCRIPT_STATUS        = `ps -ef | grep $RUNNINGSCRIPT | grep $KEYWORD1 |  grep $KEYWORD2 | grep $KEYWORD3 |grep -v $JOB | grep -v grep | wc -l`
set RUNNINGSCRIPT_PID           = `ps -ef | grep $RUNNINGSCRIPT | grep $KEYWORD1 |  grep $KEYWORD2 | grep $KEYWORD3 |grep -v $JOB | grep -v grep | gawk '{print $2}'`
#
# CHECK if another str2str is running with the same configuration
if ( $RUNNINGSCRIPT_STATUS) then
        echo $RUNNINGSCRIPT is runnning with PID $RUNNINGSCRIPT_PID. Stop it before restarting!
else
        echo $RUNNINGSCRIPT is stopped... Restarting now!
	#******************** START TO DUMP HOURLY FILES ************************
	# capture pos streams from TCP local port 5754 (generated by rtkrcv) to a local file
	nohup /usr/local/bin/str2str -in tcpcli://${IP_ADDR}:${IP_PORT} -out file://${dumpDir}/%Y/%n/%H/${SITE}.%Y.%m.%d.%n.%H.pos::S=1::T -f 0>& /dev/null &

endif

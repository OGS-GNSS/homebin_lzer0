#!/bin/tcsh -f
# created  by DZ (Feb 2017)
# modified by DZ (Aug 2022) 
# modified by sg (mar 25) - Added internal logging
# manual starts rtkrcv to avoid the segmentation faul issue of rtklib 2.4.3 b26
#
#********************  DEFAULTS ********************
set JOB         		= `basename $0`
set CURDIR      		= `pwd`
set RUNNINGSCRIPT 		= rtkrcv
set KEYWORD1 			= 2950
set KEYWORD2 			= rtkrcv.curr.conf
set RUNNINGSCRIPT_STATUS        = `ps -ef | grep $RUNNINGSCRIPT | grep $KEYWORD1 |  grep $KEYWORD1 | grep -v $JOB | grep -v grep | wc -l`
set RUNNINGSCRIPT_PID           = `ps -ef | grep $RUNNINGSCRIPT | grep $KEYWORD1 |  grep $KEYWORD1 | grep -v $JOB | grep -v grep | gawk '{print $2}'`
set LOG_DIR                     = $HOME/log
set LOG_FILE                    = $LOG_DIR/lzer0.start.rtk.log

# Create log directory if it doesn't exist
if (! -d $LOG_DIR) then
    mkdir -p $LOG_DIR
    echo "`date`: Created log directory $LOG_DIR" >> $LOG_FILE
endif

# Function to log messages
alias log 'echo "`date`: \!*" >> $LOG_FILE'

log "Script started"
#
# If no arguments are passed it outputs script usage
if (${%argv} == 0) then
        echo "- USAGE: $JOB -f [config file]"
        echo "    e.g: $JOB -f $HOME/cfg/rtkrcv.curr.conf"
        log "No arguments provided, exiting"
        exit 1
endif
#
# check command line params.
while($#)
        switch($1)
                case "-f":
                        shift
                        set cfgFile	= $1
                        log "Configuration file set to $cfgFile"
                        breaksw
                default:
                        breaksw
        endsw
        shift
end
#
# CHECK if another rtkrcv is running with the same telnet port and configuration
if ( $RUNNINGSCRIPT_STATUS) then
        echo $RUNNINGSCRIPT is runnning with PID $RUNNINGSCRIPT_PID. Stop it before restarting!
        log "$RUNNINGSCRIPT is already running with PID $RUNNINGSCRIPT_PID. Exiting."
else
        echo $RUNNINGSCRIPT is stopped... Restarting now!
        log "$RUNNINGSCRIPT is stopped. Restarting now."
        
        # Change to log directory for trace files
        cd $LOG_DIR
        log "Changed directory to $LOG_DIR"
        
        # Start rtkrcv
        nohup /usr/local/bin/rtkrcv -p 2950 -o $cfgFile & 
        log "Started rtkrcv with configuration $cfgFile"

        sleep 3
        log "Sleeping for 3 seconds before manual start"
        
        ## manual start
        log "Attempting manual start via telnet"
        (echo admin; sleep 1; \
                echo "start\r\n"; sleep 1; \
                echo "exit\r\n"; sleep 1) | telnet localhost 2950
        
        log "Manual start completed"
endif

log "Script execution completed"
